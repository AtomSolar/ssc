/* example script to test waterproperties dynamic library 
   aron dobos, october 2012
*/

folder = (ostype()=='win32') ? 'c:/Users/adobos/Projects/tcs/waterprop/' : '/Users/adobos/Projects/tcs/waterprop/';
bits = (ostype()=='win32') ? "32" : "64";
waterdll = folder + 'waterprop' + bits + '.' + ( (ostype()=='win32')?'dll':'dylib' );
dllok = load_extension(waterdll);
if (!dllok)
{
	outln('failed to load:' + waterdll );
	exit;
}

ext = extensions();
libs = @ext;
for ( i=0;i<#libs;i++)
{
	outln( libs[i] );
	funcs = @ext{libs[i]};
	for ( j=0;j<#funcs;j++)
		outln( '  ' + funcs[j] + '  -->  ' + ext{libs[i]}{funcs[j]}.signature + '    ' + ext{libs[i]}{funcs[j]}.description );
}


newplot(true);

function create_Ts_diagram()
{
	pmax = 20000;
	pmin = 500;
	for (p = pmin; p<=pmax; p=p+(pmax-pmin)/20 )
	{
		svec = 0;
		tvec = 0;
		for (i = 0; i<100; i++ )
		{
			svec[i] = (i+1)/15.0 + 1.0;
			props = water_PS( p, svec[i] );
			if ( typeof(props) == 'table' ) tvec[i] = props.T;
			else tvec[i] = 0.0;
		}
		
		plot( svec, tvec, {'series'=p+' kPa', 'color'=[0, 100, 254.0*(p-pmin)/(pmax-pmin)] } );
	}


	plotopt({'legend'=true, 'legendpos'='right', 'title'='Water properties T-s', 'window'=[20,20,640,480] } );

	axis('x1', {'label'='s (kJ/kg-K)'});
	axis('y1', {'label'='T (C)'} );
	plotpng( folder + 't-s.png' );
}

function create_Pv_diagram()
{
	newplot();
	Tmax = 400;
	Tmin = 50;
		
	for (T = Tmin; T<=Tmax;T=T+(Tmax-Tmin)/20)
	{
		pvec = 0;
		vvec = 0;
		for (i=0;i<100;i++)
		{
			pvec[i] = 100 + (i/100)*22000;
			props = water_TP( T, pvec[i] );
			if ( typeof(props) == 'table'  && props.V > 0.0 ) vvec[i] = props.V;
			else vvec[i] = 0.0;
				
			pvec[i] = pvec[i]*0.001;
		}
		
		plot( vvec, pvec, {'series'=T+' C', 'color'=[100,0,254*(T-Tmin)/(Tmax-Tmin)] } );
	}
	
	plotopt({'legend'=true, 'legendpos'='right', 'title'='Water properties P-v', 'window'=[680,20,640,480] } );
	
	axis('x1', {'type'='log', 'label'='v (m3/kg)'});
	axis('y1', {'label'='P (MPa)'} );
	plotpng( folder + 'P-v.png' );
}

function create_Mollier_diagram()
{
	newplot();
	
	//T = [ 100, 200, 300, 400 ];
	P = [ 10, 50, 100,200,500, 1000, 2000, 4000, 5000, 6000, 7000 ];
	
	for ( i=0;i<#P;i++)
	{
		pres = P[i];
		for ( j=0;j<100;j++ )
		{
			entr[j] = 4*j/100.0+4.5;
			props = water_PS( pres, entr[j] );
			if ( typeof(props) == 'table') enth[j] = props.H;
			else enth[j] = 0;
		}
		plot( entr, enth, {'series'=pres+' kPa', 'color'=[(i+1)/(#P+1)*254, 0, 0]});
	}
	plotopt({'legend'=true, 'legendpos'='right', 'title'='Mollier diagram', 'window'=[20,500,640,480]});
	axis('x1', {'label'='s (kJ/kg-K)'});
	axis('y1', {'label'='h (kJ/kg)'});
	plotpng( folder + 'mollier.png' );
}


// create plots
create_Ts_diagram();
create_Pv_diagram();
create_Mollier_diagram();